buildscript {
    ext {
        buildToolsVersion = "33.0.0"
        minSdkVersion = 26
        compileSdkVersion = 33
        targetSdkVersion = 33
        kotlinVersion = "1.8.10"
        reactNativeVersion = "0.72.17"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle:8.1.1")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
    }
}

allprojects {
    repositories {
        // ใช้มาตรฐานแม่บท: ให้ทุกโปรเจกต์มองเห็น node_modules ผ่าน Root
        maven { url "$rootDir/../node_modules/react-native/android" }
        maven { url "$rootDir/../node_modules/jsc-android/dist" }
        google()
        mavenCentral()
        maven { url 'https://www.jitpack.io' }
    }

    configurations.all {
        resolutionStrategy {
            force "com.facebook.react:react-android:0.72.17"
            force "com.facebook.react:hermes-android:0.72.17"
            dependencySubstitution {
                substitute module("com.facebook.react:react-native") using module("com.facebook.react:react-android:0.72.17")
            }
        }
    }
}

subprojects {
    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            project.android {
                buildFeatures { buildConfig = true }
                if (namespace == null) {
                    try {
                        def manifestFile = file("${projectDir}/src/main/AndroidManifest.xml")
                        if (manifestFile.exists()) {
                            def parsedManifest = new XmlParser().parse(manifestFile)
                            namespace = parsedManifest."@package"
                        }
                    } catch (Exception e) {
                        namespace = "com.fix.${project.name.replaceAll(/[^\w]/, "")}"
                    }
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }
            }
        }
    }
}